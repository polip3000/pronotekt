import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.serialization)
    id 'maven-publish'
}
group = "com.github.polip3000"
version = "1.0.0"

android {
    namespace = "fr.algorythmice.pronotekt"

    compileSdk {
        version = release(36)
    }

    defaultConfig {
        minSdk = 24

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(
                getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard-rules.pro'
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        coreLibraryDesugaringEnabled true
    }
    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_11)
        }
    }
    publishing {
        singleVariant("release")
    }
}

dependencies {
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.appcompat)
    implementation(libs.material)

    api(libs.ktor.serialization.kotlinx.json)
    api(libs.jsoup)
    api(libs.okhttp)
    api(libs.bcprov.jdk15to18.v1781)
    api(libs.bcpkix.jdk15to18)
    api(libs.json)

    coreLibraryDesugaring(libs.desugar.jdk.libs)
    implementation libs.kotlin.reflect
}
def androidSourcesJar = tasks.register('androidSourcesJar', org.gradle.jvm.tasks.Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            create("release", org.gradle.api.publish.maven.MavenPublication) {
                from(components["release"])
                artifact(androidSourcesJar.get().archiveFile) {
                    builtBy(androidSourcesJar)
                }
                groupId = project.group.toString()
                artifactId = "Pronotekt"
                version = project.version.toString()
            }
        }
        repositories {
            maven {
                name = "localMaven"
                url = uri(rootProject.layout.buildDirectory.dir("localMaven").get().asFile)
            }
        }
    }
}
